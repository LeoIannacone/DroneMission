ContactSystem DroneMissionSystem -awt -o spaceUpdater [host="localhost" port=8070];

Context subSystemDrone -w;
Context subSystemHeadQuarter -w;
Context subSystemSmartDevice -w;

//		Subsystems -w (window)
Subject smartdevice context subSystemSmartDevice ; 
Subject drone context subSystemDrone ;
Subject headQuarter context subSystemHeadQuarter ; 

// Just for testing
//Context ctxTesting -w;
//Subject smartdevice context ctxTesting ; 
//Subject drone context ctxTesting ;
//Subject headQuarter context ctxTesting ; 

//		Messages
Dispatch photo; 
Request command;
Signal sensorsData;
Signal notify;

//		Highlevel communications
//Drone sends photo
sendDataPhoto: drone forward photo to headQuarter;  
//Headquarter receives photo
receiveDataPhoto: headQuarter serve photo support=TCP [host="localhost" port=4060];

//Drone sends data of sensors
sendsensorsDatas: drone emit sensorsData;
//Headquarter and smartdevice receive data of sensors
headquarterReceivesensorsDatas: headQuarter sense sensorsData;
smartdeviceReceivesensorsDatas: smartdevice sense sensorsData;

//Drone notifies start/end mission
sendnotify: drone emit notify;
//smartdevice receives notifications about mission
smartdeviceReceiveNotify: smartdevice sense notify;

//HeadQuarter sends command
sendCommand: headQuarter demand command to drone;
//Drone receives command
receiveCommand: drone grant command support=TCP [host="localhost" port=4050];


//		Behaviors 
//Behavior of Drone
BehaviorOf drone {
	var String msgCommand = ""
	var String cmdName = ""
	var String cmdValue = ""

	var boolean start
	var boolean stop
	var boolean speed

	var String sensorsDatas
	var String dataPhoto

	action void startMission()
	action void endMission()
	action void setSpeed(String value)
	
	action String getDataFromSensors()
	action String getDataPhoto()

	state st_Drone_init initial
		println ("----- Drone Initialized -----")
		goToState st_Drone_ready
	endstate
	
	state st_Drone_ready
		println ("----- Waiting setSpeed -----")
		doInOut receiveCommand()
		set msgCommand = call curInputMsg.msgContent()
		set cmdName = call Drone.getCommandName(msgCommand)
		set start = call cmdName.contains("setspeed")
		if{start} {	
			replyToRequest command("OK"); 
			goToState st_Drone_startMission
		}
		replyToRequest command("FAIL");
		println ("ERROR: expected 'setspeed' command to start. Received: " + cmdName)
	endstate
	
	state st_Drone_startMission
		exec startMission() // empty method - maybe can be used in future?
		doOut sendnotify("start")
		goToState st_Drone_setspeed
	endstate
	
	state st_Drone_setspeed
		set cmdValue =  call Drone.getCommandValue(code.curInputMsgContent)
		exec setSpeed(cmdValue)
		goToState st_Drone_onMission
	endstate

	state st_Drone_onMission
		// send data sensors
		set sensorsDatas = exec getDataFromSensors()
		doOut sendsensorsDatas(sensorsDatas)

		// send photos
		set dataPhoto = exec getDataPhoto()
		doOut sendDataPhoto(dataPhoto) // every x secondi?

		// received a command setSpeed or stop?
		onMessage? command goToState st_Drone_commandHandler
	endstate

	state st_Drone_commandHandler
		doInOut receiveCommand()
		set cmdName = call Drone.getCommandName(code.curInputMsgContent)
		
		// check if command is correct
		set stop = call cmdName.contains("stop")
		set speed = call cmdName.contains("setspeed")
		if { stop || speed } {
			replyToRequest command ("OK");
			if {stop} { goToState st_Drone_endMission };
			if {speed} { goToState st_Drone_setspeed }
		}
		replyToRequest command("FAIL");
		// get back on mission
		goToState st_Drone_onMission
	endstate
	
	state st_Drone_endMission
		exec endMission() // empty method - maybe can be used in future?
		doOut sendnotify("end")
		transitToEnd
	endstate
}

BehaviorOf headQuarter{
	
	var String command
	var String sensorsDatasReceived
	var String photoReceived
	var String commandAnswer
	
	action String getCommandToSend()
	action void updateDashboard(String sensorsDatasReceived)
	action void storePhotoData(String photoReceived)
	action void shutdown()
	
	state st_HeadQuarter_init initial
		println ("----- HeadQuarter Initialized -----") 
		goToState st_HeadQuarter_ready
	endstate
	
	state st_HeadQuarter_ready
		println ("----- Ready to send command -----")
		doOutIn sendCommand("setspeed 60")
		acquireAnswerFor command
		set commandAnswer = code.curReplyContent
		println("DRONE REPLY: " + commandAnswer)
		if { commandAnswer == "OK" } { goToState st_HeadQuarter_onMission }
	endstate
	
	state st_HeadQuarter_onMission
		// check if command is clicked in Dashboard and send it
		set command = exec getCommandToSend()
		doOutIn sendCommand(command)
		acquireAnswerFor command
		set commandAnswer = code.curReplyContent
		if { commandAnswer == "FAIL" } { println("DRONE CMD FAILED: " + commandAnswer) }
		
		if { commandAnswer == "OK" && command == "stop"} { 
			goToState st_HeadQuarter_endMission
		}
				
		// get sensors data
		onMessage? sensorsData goToState st_HeadQuarter_receivedSensorsData
		
		// get photos
		onMessage? photo goToState st_HeadQuarter_receivedPhoto
		
	endstate
	
	state st_HeadQuarter_receivedSensorsData
		// get sensors data
		doPerceive headquarterReceivesensorsDatas()
		set sensorsDatasReceived = code.curInputMsgContent
		// update Dashboard
		call updateDashboard(sensorsDatasReceived)

		goToState st_HeadQuarter_onMission
	endstate
	
	state st_HeadQuarter_receivedPhoto
		// get photo data
		doIn receiveDataPhoto()
		set photoReceived = code.curInputMsgContent
		
		// store info
		call storePhotoData(photoReceived)
		
		goToState st_HeadQuarter_onMission
	endstate
	
	state st_HeadQuarter_endMission
		call shutdown()
		transitToEnd
	endstate
}

BehaviorOf smartdevice {
	
	var String notifyContent
	var String dataDroneReceived 
	
	action void notifyUserMissionStarted()
	action void showSensorsDatasReceived(String data)
	action void missionFinished()
	
	state st_Smartdevice_init initial
		onMessage notify transitTo st_Smartdevice_missionStart
	endstate
	
	state st_Smartdevice_missionStart
		call notifyUserMissionStarted()
		goToState st_Smartdevice_waitingForData
	endstate
	
	state st_Smartdevice_waitingForData
		onMessage? sensorsData goToState st_Smartdevice_receivedData
		onMessage? notify goToState st_Smartdevice_endMission
	endstate
	
	state st_Smartdevice_receivedData
		// get data from drone
		doPerceive smartdeviceReceivesensorsDatas()
		set dataDroneReceived = code.curInputMsgContent
		call showSensorsDatasReceived(dataDroneReceived)

		goToState st_Smartdevice_waitingForData
	endstate
	
	state st_Smartdevice_notifyHandler
		doPerceive smartdeviceReceiveNotify()
		set notifyContent = code.curInputMsgContent
		if {notifyContent == "start"} { goToState st_Smartdevice_missionStart }
		if {notifyContent == "end" } { goToState st_Smartdevice_endMission }
		goToState st_Smartdevice_waitingForData
	endstate
	
	state st_Smartdevice_endMission
		call missionFinished()
		transitToEnd
	endstate
}
